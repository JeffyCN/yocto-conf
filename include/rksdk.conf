include rksdk/alsa-config.conf
include rksdk/gstreamer-rockchip.conf
include rksdk/kernel.conf
include rksdk/librga.conf
include rksdk/mpp.conf
include rksdk/rkbin.conf
include rksdk/rkwifibt.conf
include rksdk/u-boot.conf

include ${@'' if d.getVar('MALI_GPU') == '' else 'rksdk/libmali.conf'}
include ${@'rksdk/rkisp.conf' if d.getVar('RK_ISP_VERSION') == '1' else ''}
include ${@oe.utils.version_less_or_equal('RK_ISP_VERSION', '1', '', 'rksdk/rkaiq.conf', d)}
include ${@bb.utils.contains('MACHINE_EXTRA_RRECOMMENDS', 'rockchip-npu', 'rksdk/rknpu.conf', '', d)}

# For NTP time-sync
IMAGE_INSTALL:append = " ntp"

# For supporting "PARTLABEL=" in fstab
IMAGE_INSTALL:append = " util-linux"

# For resizing extra partitions
IMAGE_INSTALL:append = " e2fsprogs-mke2fs e2fsprogs-resize2fs e2fsprogs-tune2fs"

# For pm-utils hooks, e.g. power-key suspend
IMAGE_INSTALL:append = " pm-utils"

# Allow using rksdk's local prebuilt u-boot images
RK_ALLOW_PREBUILT_UBOOT = "1"

RK_OVERLAY_DIRS:append = " \
	${TOPDIR}/../../device/rockchip/common/data/yocto \
"

RK_POST_ROOTFS_SCRIPTS:append = " \
	${TOPDIR}/../../device/rockchip/common/post-build.sh \
"

PSEUDO_IGNORE_PATHS .=",${TOPDIR}/../../output,${TOPDIR}/../../external/rkwifibt"
